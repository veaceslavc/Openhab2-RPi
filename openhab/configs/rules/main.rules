
// Generic

var Timer Liv_Temp_DelayedTimer
var DecimalType liv_delayedTemp
var Number outside_raw_prev
var boolean ledState

//const WALLMOTE_INCDEC=5

rule "Initialize items"
when
    System started
then
    logInfo("Initializer", "Started Timer ...")

	Liv_Temp_DelayedCaption.postUpdate("Not Set")
	TH_GF_HCLivMode.sendCommand("Auto")
	TH_GF_HCBedMode.sendCommand("Auto")
	Heating_Controls_Active.sendCommand("ON")
	ledState = true

    createTimer(now.plusSeconds(30)) [|

        logInfo("Initializer", "Initializing default values")
        //Wait for 30 seconds and initialize items 
        if (Liv_Temp_DelayedTemp.state == NULL) Liv_Temp_DelayedTemp.postUpdate(20)
        if (Liv_Temp_DelayedInterval.state == NULL) Liv_Temp_DelayedInterval.postUpdate(4)
        
    ]
end

// LED heartbeat 
rule "LED Heartbeat"
when
	Time cron "0/5 * * ? * *"
then
	if (ledState) {
		StatusLED.sendCommand(ON)
	} else {
		StatusLED.sendCommand(OFF)
	}
	//var State newState = ledState ? OnOffType.ON : OnOffType.OFF
	ledState = !ledState
end


// Outside 

rule "Smooth Outside Temp"
when
  Item Outside_Temp_Raw received update
then
	var outside_raw = Outside_Temp_Raw.state as Number
	// logInfo("ThingStatus", "Outside_Temp_Raw.state = " + outside_raw)
	if ( Outside_Temp.state == null || outside_raw_prev == null || outside_raw == null) {
		// change Outside_Temp if not initialized or diff is less than 1 degree
		// logInfo("ThingStatus", "Updated Outside_Temp.")
	  	Outside_Temp.postUpdate(outside_raw)
	} else if ( Math::abs( outside_raw.floatValue - outside_raw_prev.floatValue) <= 1 ){
		// logInfo("ThingStatus", "Updated Outside_Temp.")
	  	Outside_Temp.postUpdate(outside_raw)
	} else {
		// logInfo("ThingStatus", "Skipped Outside_Temp.")
	}
	outside_raw_prev = outside_raw
end

rule "Records last Outside Temp update time"
when
  Item Outside_Temp received update
then
  postUpdate(Outside_Temp_LastUpdate, new DateTimeType())
end


//  Living Room 

rule "Set Predefined Temp in Living room"
when
	Item Liv_Temp_Predefined received command
then
	logDebug("TempLogger", "Liv_Temp_Predefined changed. Liv_Temp_Predefined.state: [{}]", Liv_Temp_Predefined.state) 
	sendCommand(Liv_Temp_Setpoint, transform("MAP", "predefined_temp.map", Liv_Temp_Predefined.state.toString))
end

rule "UnSet Predefined Temp in Living room"
when
	Item Liv_Temp_Setpoint changed
then
	var setpointToPredefined = transform("MAP", "predefined_temp_reverse.map", Liv_Temp_Setpoint.state.format("%2.1f")) 
	if (setpointToPredefined !== null && setpointToPredefined.length > 0) {
		postUpdate(Liv_Temp_Predefined, setpointToPredefined)
	} else {
		postUpdate(Liv_Temp_Predefined, NULL)
	}
		
end

// Creates an item that stores the status of Livving Room Thermostat

rule "Liv Thermostat Status"
when
	System started or
    Thing 'zwave:device:66993751:node2' changed
then
	var thingStatusInfo = getThingStatusInfo("zwave:device:66993751:node2")

	// if ((thingStatusInfo != null) && (thingStatusInfo.getStatus().toString() == "ONLINE")) {
	if (thingStatusInfo !== null) {
		Liv_Thermostat_Status.sendCommand(thingStatusInfo.getStatus().toString())
		logInfo("ThingStatus", "Liv_Thermostat is online.")
	} else {
		Liv_Thermostat_Status.sendCommand("UNKNOWN")
	}
end

rule	"WallMote Control"
when
	Item Wallmote_SceneNumber received update
then
	logInfo("Wallmote_SceneNumber", "Wallmote_SceneNumber got scene # " + Wallmote_SceneNumber.state )
	switch Wallmote_SceneNumber.state {
		case 1.0: {
			Bedroom_Dimmer.sendCommand(100)
    	}
		case 2.0: {
			Bedroom_Lamp.sendCommand(100)
    	}
		case 3.0: {
			Bedroom_Dimmer.sendCommand(OFF)
    	}
		case 4.0: {
			Bedroom_Lamp.sendCommand(OFF)
    	}
		case 1.2: {
			if ( (Bedroom_Dimmer.state as Number) < 95 ) {
				Bedroom_Dimmer.sendCommand((Bedroom_Dimmer.state as Number) + 5)
			} else {
				Bedroom_Dimmer.sendCommand(100);
			}
    	}
		case 2.2: {
			if ( (Bedroom_Lamp.state as Number) < 95 ) {
				Bedroom_Lamp.sendCommand((Bedroom_Lamp.state as Number) + 5)
			} else {
				Bedroom_Lamp.sendCommand(1);
			}
    	}
		case 3.2: {
			if ( (Bedroom_Dimmer.state as Number) > 5 ) {
				Bedroom_Dimmer.sendCommand((Bedroom_Dimmer.state as Number) - 5)
			} else {
				Bedroom_Dimmer.sendCommand(1);
			}
    	}
		case 4.2: {
			if ( (Bedroom_Lamp.state as Number) > 5 ) {
				Bedroom_Lamp.sendCommand((Bedroom_Lamp.state as Number) - 5)
			} else {
				Bedroom_Lamp.sendCommand(1);
			}
    	}
	}	
end

rule "Say welcome "
when
    Item RF433_Socket2 changed to ON
then
    //Alexa_Living_Room_TTS.sendCommand('Olga, Slava is coming for lunch.')
end


// This rule will be used to test Scale transformation service
/*
rule "Compute Liv humidex"
when
    Item Liv_Temperature changed
then
	var Number T = Liv_Temperature.state as DecimalType
	var Number H = Liv_Humidity.state as DecimalType	
	var Number x = 7.5 * T/(237.7 + T)
	var Number e = 6.112 * Math::pow(10, x.doubleValue) * H/100
	var Number humidex = T + (new Double(5) / new Double(9)) * (e - 10)
	postUpdate(Liv_Humidex, humidex)
end
*/

rule "LivRoom Delayed Temp set"
when
    Item Liv_Temp_DelayedOnOff changed from 0 to 1
then
	logInfo("Delay Temp", "Timer activated")
	
	liv_delayedTemp = Liv_Temp_DelayedTemp.state as DecimalType
	
	val org.eclipse.xtext.xbase.lib.Functions$Function2 makeTimer = [
        int myDelay,
        org.eclipse.xtext.xbase.lib.Functions$Function2 makeTimer2 |
			logInfo("Delay Temp", "Inside makeTimer, myDelay=" + myDelay)
            if(myDelay>0) {
				Liv_Temp_DelayedCaption.sendCommand(String::format("%.1f Â°C in %.1f h", liv_delayedTemp.floatValue(), (myDelay / 60.0)))
                Liv_Temp_DelayedTimer = createTimer(now.plusMinutes(1)) [| 
                    makeTimer2.apply(myDelay-1, makeTimer2)
                ]
            } else {
                //timer reach zero
				logInfo("Delay Temp", "Delayed set: Setting temp to " + liv_delayedTemp)
 				Liv_Temp_Setpoint.sendCommand(liv_delayedTemp)
 				Liv_Temp_DelayedOnOff.sendCommand("0")
            }
    ]

    //start the timer
    var intervalMinutes = ((Liv_Temp_DelayedInterval.state as DecimalType) * 60).intValue
    makeTimer.apply(intervalMinutes ,makeTimer)
	
end


rule "LivRoom Delayed Temp set"
when
    Item Liv_Temp_DelayedOnOff changed from 1 to 0
then
	logInfo("Delay Temp", "Timer deactivated")
	if (Liv_Temp_DelayedTimer !== null) {
		Liv_Temp_DelayedTimer.cancel()
		Liv_Temp_DelayedTimer = null
	}
	Liv_Temp_DelayedCaption.sendCommand("Not Set")  
end


// ====================================================================
//  BedRoom 
// ====================================================================

rule "Set Predefined Temp in bedroom"
when
	Item Bed_Temp_Predefined received command
then
	logDebug("TempLogger", "Bed_Temp_Predefined changed. Bed_Temp_Predefined.state: [{}]", Bed_Temp_Predefined.state) 
	sendCommand(BedTemp_Setpoint, transform("MAP", "predefined_temp.map", Bed_Temp_Predefined.state.toString))
end

rule "UnSet Predefined Temp in bedroom"
when
	Item BedTemp_Setpoint changed
then
	// logDebug("TempLogger", "BedTemp_Setpoint changed. BedTemp_Setpoint.state: [{}]. BedTemp_Setpoint.previousState: [{}]", BedTemp_Setpoint.state, previousState)
	var setpointToPredefined = transform("MAP", "predefined_temp_reverse.map", BedTemp_Setpoint.state.toString) 
	if (setpointToPredefined !== null && setpointToPredefined.length > 0)
		postUpdate(Bed_Temp_Predefined, setpointToPredefined)
	else
		postUpdate(Bed_Temp_Predefined, NULL)
		
end

// Creates an item that stores the last update time of this item
rule "Records last bedroom Temp update time"
when
  Item Bed_Temperature received update
then
  postUpdate(Bed_Temp_LastUpdate, new DateTimeType())
end

